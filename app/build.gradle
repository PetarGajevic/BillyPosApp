plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.wind.billypos"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        try {
            Properties props = new Properties()
            props.load(new FileInputStream("config.properties"))
            props.each { prop -> project.ext.setProperty(prop.key, prop.value) }
        } catch (exception) {
            throw new Exception("Configuration not found : $exception.message")
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            dimension "default"
            buildConfigField("String", "BASE_URL", "\"${BASE_URL_DEV}\"")
            buildConfigField("String", "BASE_TAX_URL", "\"${BASE_TAX_URL_DEV}\"")
            buildConfigField("String", "BASE_QR_URL", "\"${BASE_QR_DEV}\"")
            buildConfigField("String", "DATE_TIME_VAT_FORMAT", "\"${DATE_TIME_VAT_FORMAT}\"")
        }
        prod {
            dimension "default"
            buildConfigField("String", "BASE_URL", "\"${BASE_URL_PROD}\"")
            buildConfigField("String", "BASE_TAX_URL", "\"${BASE_TAX_URL_PROD}\"")
            buildConfigField("String", "BASE_QR_URL", "\"${BASE_QR_PROD}\"")
            buildConfigField("String", "DATE_TIME_VAT_FORMAT", "\"${DATE_TIME_VAT_FORMAT}\"")
        }
    }

    buildFeatures{
        dataBinding {
            enabled = true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.4'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Room
    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.room:room-rxjava2:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'

    /** XML **/
    implementation 'com.tickaroo.tikxml:annotation:0.9.0_11-SNAPSHOT'
    implementation 'com.tickaroo.tikxml:core:0.9.0_11-SNAPSHOT'
    implementation 'com.tickaroo.tikxml:retrofit-converter:0.9.0_11-SNAPSHOT'
    kapt 'com.tickaroo.tikxml:processor:0.9.0_11-SNAPSHOT'
    implementation group: 'org.apache.santuario', name: 'xmlsec', version: '1.5.2'
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    implementation group: 'xerces', name: 'xercesImpl', version: '2.12.0'


    /** Other **/
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'
    implementation ("com.github.bumptech.glide:glide:4.11.0") {
        exclude group: "com.android.support"
    }
    kapt "com.github.bumptech.glide:compiler:4.11.0"


    /** Square Up **/
    implementation 'com.squareup.retrofit2:retrofit:2.6.4'
    implementation ("com.squareup.okhttp3:okhttp:3.12.12"){
        version { strictly '3.12.12'  }
    }
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.12'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'

    /** Reactive X **/
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'

    //Koin
    implementation "org.koin:koin-android:2.0.1"
    implementation 'org.koin:koin-androidx-viewmodel:2.0.1'
    implementation 'org.koin:koin-androidx-scope:2.0.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"

    //jetpack-preference
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation "androidx.preference:preference-ktx:1.1.0-rc01"

    // Multi dex
    implementation 'com.android.support:multidex:1.0.3'

    // QR CODE Generator
    implementation 'com.github.kenglxn.QRGen:android:2.6.0'

    // Printer
    implementation 'com.github.DantSu:ESCPOS-ThermalPrinter-Android:3.0.0'

}